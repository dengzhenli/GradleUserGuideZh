# 处理随时间变化的版本


内容

  * [声明动态版本](#sub:declaring_dependency_with_dynamic_version)
  * [声明版本变更](#sub:declaring_dependency_with_changing_version)
  * [控制动态版本缓存](#sec:controlling-dynamic-version-caching)
  * [以编程方式控制依赖项缓存](#sec:controlling_dependency_caching_programmatically)
  * [从命令行控制依赖项缓存](#sec:controlling_dependency_caching_command_line)
  * [使用组件选择规则](#sec:component_selection_rules)

在许多情况下，您想使用特定模块依赖项的最新版本或一系列版本中的最新版本。这可能是开发期间的要求，或者您可能正在开发旨在与一系列依赖版本一起使用的库。您可以使用
_动态版本_
轻松地依赖这些不断变化的依赖关系。一个[动态版本](#sub:declaring_dependency_with_dynamic_version)可以是一个版本范围（例如`2.+`），也可以是最新的版本可用例如一个占位符`latest.integration`。

另外，您请求的模块甚至可以针对同一版本（即所谓的“[更改版本”）](#sub:declaring_dependency_with_changing_version)随时间[变化](#sub:declaring_dependency_with_changing_version)。此类
_更改模块的_
一个示例是Maven`SNAPSHOT`模块，该模块始终指向最新发布的工件。换句话说，标准的Maven快照是一个不断发展的模块，它是一个“不断变化的模块”。

╔═════════════════════════════  

使用动态版本和更改模块可能导致无法复制的构建。随着特定模块的新版本发布，其API可能与您的源代码不兼容。请谨慎使用此功能！  
  
╚═════════════════════════════    
  
## [](#sub:declaring_dependency_with_dynamic_version)[声明动态版本](#sub:declaring_dependency_with_dynamic_version)

项目可能会采用更积极的方法来消耗对模块的依赖。例如，您可能希望始终集成最新版本的依赖项，以便在任何给定时间使用最先进的功能。一个 _动态版本_
可以解决的最新版本的或一系列的最新版本为给定的模块。

╔═════════════════════════════  

在构建中使用动态版本会承担潜在破坏它的风险。包含不兼容的API更改的依赖关系的新版本发布后，您的源代码可能会停止编译。  
  
╚═════════════════════════════    
  
例子1.声明一个动态版本的依赖

`Groovy``Kotlin`

build.gradle

    
    
    plugins {
        id 'java-library'
    }
    
    repositories {
        mavenCentral()
    }
    
    dependencies {
        implementation 'org.springframework:spring-web:5.+'
    }

build.gradle.kts

    
    
    plugins {
        `java-library`
    }
    
    repositories {
        mavenCentral()
    }
    
    dependencies {
        implementation("org.springframework:spring-web:5.+")
    }

一个[构建扫描](https://scans.gradle.com/)可以有效的可视化动态相关版本和它们各自的，选定的版本。

![依赖管理动态依赖构建扫描](img/dependency-management-dynamic-dependency-build-scan.png)

图1.构建扫描中的动态依赖关系

默认情况下，Gradle将动态版本的依赖项缓存24小时。在此时间范围内，Gradle不会尝试从声明的存储库中解析较新的版本。该[阈值可以被配置](#sec:controlling-
dynamic-version-caching)为需要，例如，如果您想更早解决新版本。

## [](#sub:declaring_dependency_with_changing_version)[声明版本变更](#sub:declaring_dependency_with_changing_version)

在发布应用程序或库的新版本之前，团队可能会决定实施一系列功能。允许消费者尽早集成其工件的未完成版本的常见策略通常是发布具有所谓 _更改版本_
的模块。版本更改表示该功能集仍在积极开发中，尚未发布稳定的版本以实现普遍可用性。

在Maven存储库中，更改的版本通常称为[快照版本](https://maven.apache.org/guides/getting-
started/index.html#What_is_a_SNAPSHOT_version)。快照版本包含后缀`-SNAPSHOT`。以下示例演示了如何在Spring依赖项上声明快照版本。

例子2.声明一个版本变化的依赖

`Groovy``Kotlin`

build.gradle

    
    
    plugins {
        id 'java-library'
    }
    
    repositories {
        mavenCentral()
        maven {
            url 'https://repo.spring.io/snapshot/'
        }
    }
    
    dependencies {
        implementation 'org.springframework:spring-web:5.0.3.BUILD-SNAPSHOT'
    }

build.gradle.kts

    
    
    plugins {
        `java-library`
    }
    
    repositories {
        mavenCentral()
        maven {
            url = uri("https://repo.spring.io/snapshot/")
        }
    }
    
    dependencies {
        implementation("org.springframework:spring-web:5.0.3.BUILD-SNAPSHOT")
    }

默认情况下，Gradle将更改的依赖项版本缓存24小时。在此时间范围内，Gradle不会尝试从声明的存储库中解析较新的版本。该[阈值可以被配置](#sec:controlling-dynamic-version-caching)为需要，例如，如果你想提前解决新的快照版本。

Gradle足够灵活，可以将任何版本视为更改版本，例如，如果您想为Ivy模块的快照行为建模。您需要做的就是将属性[ExternalModuleDependency.setChanging（boolean）设置](https://docs.gradle.org/6.7.1/javadoc/org/gradle/api/artifacts/ExternalModuleDependency.html#setChanging-boolean-)为`true`。

## [](#sec:controlling-dynamic-version-caching)[控制动态版本缓存](#sec:controlling-dynamic-version-caching)

默认情况下，Gradle会在24小时内缓存动态版本和更改模块。在这段时间内，Gradle不会与任何已声明的远程存储库联系以获取新版本。如果您希望Gradle更频繁地检查远程存储库或在每次执行构建时检查远程存储库，则需要更改生存时间（TTL）阈值。

╔═════════════════════════════  

由于HTTP（s）调用的数量增加，因此对于动态版本或更改版本使用较短的TTL阈值可能会导致构建时间更长。  
  
╚═════════════════════════════    
  
您可以使用[命令行选项](#sec:controlling_dependency_caching_command_line)覆盖默认的缓存模式。您还可以使用解析策略以[编程方式更改构建中的缓存过期时间](#sec:controlling_dependency_caching_programmatically)。

## [](#sec:controlling_dependency_caching_programmatically)[以编程方式控制依赖项缓存](#sec:controlling_dependency_caching_programmatically)

您可以使用[ResolutionStrategy](https://docs.gradle.org/6.7.1/javadoc/org/gradle/api/artifacts/ResolutionStrategy.html)对配置进行编程来微调缓存的某些方面。如果您想永久更改设置，则编程方式非常有用。

默认情况下，Gradle将动态版本缓存24小时。要更改Gradle将解析后的版本缓存为动态版本的时间，请使用：

例子3.动态版本缓存控制

`Groovy``Kotlin`

build.gradle

    
    
    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor 10, 'minutes'
    }

build.gradle.kts

    
    
    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor(10, "minutes")
    }

默认情况下，Gradle会将更改的模块缓存24小时。要更改Gradle将为更改的模块缓存元数据和工件的时间，请使用：

例子4.改变模块缓存控制

`Groovy``Kotlin`

build.gradle

    
    
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 4, 'hours'
    }

build.gradle.kts

    
    
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor(4, "hours")
    }

## [](#sec:controlling_dependency_caching_command_line)[从命令行控制依赖项缓存](#sec:controlling_dependency_caching_command_line)

### [](#sec:offline-mode)[避免使用离线模式进行网络访问](#sec:offline-mode)

该`--offline`命令行开关告诉Gradle总是使用依赖模块从缓存中，而不管他们是否是由于再次检查。当离线运行时，Gradle将永远不会尝试访问网络来执行依赖关系解析。如果依赖项缓存中不存在所需的模块，则生成执行将失败。

### [](#sec:refreshing-dependencies)[刷新依赖](#sec:refreshing-dependencies)

您可以控制依赖项缓存的行为，以从命令行进行不同的构建调用。命令行选项有助于对构建的一次执行做出选择性的临时选择。

有时，Gradle Dependency
Cache可能与已配置存储库的实际状态不同步。最初可能是错误配置了存储库，或者错误地发布了“不变”模块。要刷新依赖项缓存中的所有依赖`--refresh-
dependencies`项，请在命令行上使用该选项。

该`--refresh-
dependencies`选项告诉Gradle忽略所有已缓存模块和工件的缓存条目。将针对所有已配置的存储库执行全新的解析，并重新计算动态版本，刷新模块并下载工件。但是，在可能的情况下，Gradle将在再次下载之前检查先前下载的工件是否有效。这是通过将存储库中已发布的SHA1值与现有下载的工件的SHA1值进行比较来完成的。

  * 动态依赖的新版本

  * 更改模块的新版本（使用相同版本字符串但可以具有不同内容的模块）

╔═════════════════════════════  

刷新依赖关系将导致Gradle使其列表缓存无效。然而：

  * 它将对元数据文件执行HTTP HEAD请求，但如果它们相同 _则不会重新下载它们_

  * 它将对工件文件执行HTTP HEAD请求，但如果它们相同 _则不会重新下载它们_

换句话说，刷新依赖关系 _仅_ 在您实际使用动态依赖关系 _或_ 您具有未知的变化依赖关系 _时才_
有影响（在这种情况下，您有责任正确地将其声明为Gradle作为变化依赖关系）。

认为使用`--refresh-dependencies`会强制下载依赖项是一个常见的误解。这是 **没有**
的情况下：Gradle只会执行什么严格要求刷新动态依赖。这 _可能_ 涉及下载新的清单或元数据文件，甚至是工件，但是如果没有任何更改，则影响最小。  
  
╚═════════════════════════════    
  
## [](#sec:component_selection_rules)[使用组件选择规则](#sec:component_selection_rules)

当有多个与版本选择器匹配的版本可用时，组件选择规则可能会影响应该选择哪个组件实例。规则适用于每个可用版本，并允许该规则明确拒绝该版本。这使Gradle可以忽略不满足规则设置的条件的任何组件实例。示例包括：

  * 对于动态版本，`1.+`某些版本可能会明确拒绝选择。

  * 对于静态版本，例如`1.4`，实例可能会基于额外的组件元数据（例如Ivy分支属性）而被拒绝，从而允许使用后续存储库中的实例。

通过[ComponentSelectionRules](https://docs.gradle.org/6.7.1/dsl/org.gradle.api.artifacts.ComponentSelectionRules.html)对象配置规则。将使用[ComponentSelection](https://docs.gradle.org/6.7.1/dsl/org.gradle.api.artifacts.ComponentSelection.html)对象作为参数调用配置的每个规则，该对象包含有关所考虑的候选版本的信息。调用[ComponentSelection.reject（java.lang.String）](https://docs.gradle.org/6.7.1/dsl/org.gradle.api.artifacts.ComponentSelection.html#org.gradle.api.artifacts.ComponentSelection:reject\(java.lang.String\))会导致明确拒绝给定的候选版本，在这种情况下，该候选版本将不被视为选择器。

以下示例显示了一个规则，该规则禁止模块的特定版本，但允许动态版本选择下一个最佳候选者。

例子5.组件选择规则

`Groovy``Kotlin`

build.gradle

    
    
    configurations {
        rejectConfig {
            resolutionStrategy {
                componentSelection {
                    // Accept the highest version matching the requested version that isn't '1.5'
                    all { ComponentSelection selection ->
                        if (selection.candidate.group == 'org.sample' && selection.candidate.module == 'api' && selection.candidate.version == '1.5') {
                            selection.reject("version 1.5 is broken for 'org.sample:api'")
                        }
                    }
                }
            }
        }
    }
    
    dependencies {
        rejectConfig "org.sample:api:1.+"
    }

build.gradle.kts

    
    
    configurations {
        create("rejectConfig") {
            resolutionStrategy {
                componentSelection {
                    // Accept the highest version matching the requested version that isn't '1.5'
                    all {
                        if (candidate.group == "org.sample" && candidate.module == "api" && candidate.version == "1.5") {
                            reject("version 1.5 is broken for 'org.sample:api'")
                        }
                    }
                }
            }
        }
    }
    
    dependencies {
        "rejectConfig"("org.sample:api:1.+")
    }

请注意，首先从最高版本开始应用版本选择。选择的版本将是所有组件选择规则都接受的第一个版本。如果没有任何规则明确拒绝该版本，则认为该版本已被接受。

同样，规则可以针对特定模块。必须以形式指定模块`group:module`。

例子6.具有模块目标的组件选择规则

`Groovy``Kotlin`

build.gradle

    
    
    configurations {
        targetConfig {
            resolutionStrategy {
                componentSelection {
                    withModule("org.sample:api") { ComponentSelection selection ->
                        if (selection.candidate.version == "1.5") {
                            selection.reject("version 1.5 is broken for 'org.sample:api'")
                        }
                    }
                }
            }
        }
    }

build.gradle.kts

    
    
    configurations {
        create("targetConfig") {
            resolutionStrategy {
                componentSelection {
                    withModule("org.sample:api") {
                        if (candidate.version == "1.5") {
                            reject("version 1.5 is broken for 'org.sample:api'")
                        }
                    }
                }
            }
        }
    }

组件选择规则在选择版本时也可以考虑组件元数据。可以考虑的其他可能的元数据是[ComponentMetadata](https://docs.gradle.org/6.7.1/javadoc/org/gradle/api/artifacts/ComponentMetadata.html)和[IvyModuleDescriptor](https://docs.gradle.org/6.7.1/javadoc/org/gradle/api/artifacts/ivy/IvyModuleDescriptor.html)。请注意，此额外信息可能并不总是可用，因此应检查其`null`值。

例子7.带有元数据的组件选择规则

`Groovy``Kotlin`

build.gradle

    
    
    configurations {
        metadataRulesConfig {
            resolutionStrategy {
                componentSelection {
                    // Reject any versions with a status of 'experimental'
                    all { ComponentSelection selection ->
                        if (selection.candidate.group == 'org.sample' && selection.metadata?.status == 'experimental') {
                            selection.reject("don't use experimental candidates from 'org.sample'")
                        }
                    }
                    // Accept the highest version with either a "release" branch or a status of 'milestone'
                    withModule('org.sample:api') { ComponentSelection selection ->
                        if (selection.getDescriptor(IvyModuleDescriptor)?.branch != "release" && selection.metadata?.status != 'milestone') {
                            selection.reject("'org.sample:api' must have testing branch or milestone status")
                        }
                    }
                }
            }
        }
    }

build.gradle.kts

    
    
    configurations {
        create("metadataRulesConfig") {
            resolutionStrategy {
                componentSelection {
                    // Reject any versions with a status of 'experimental'
                    all {
                        if (candidate.group == "org.sample" && metadata?.status == "experimental") {
                            reject("don't use experimental candidates from 'org.sample'")
                        }
                    }
                    // Accept the highest version with either a "release" branch or a status of 'milestone'
                    withModule("org.sample:api") {
                        if (getDescriptor(IvyModuleDescriptor::class)?.branch != "release" && metadata?.status != "milestone") {
                            reject("'org.sample:api' must have testing branch or milestone status")
                        }
                    }
                }
            }
        }
    }

请注意，在声明组件选择规则时， _始终_
需要[ComponentSelection](https://docs.gradle.org/6.7.1/dsl/org.gradle.api.artifacts.ComponentSelection.html)参数作为参数。
__

