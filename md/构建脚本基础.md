# 构建脚本基础


内容

  * [projects与tasks](#projects%EF%BC%88%E9%A1%B9%E7%9B%AE%EF%BC%89%E4%B8%8Etasks%EF%BC%88%E4%BB%BB%E5%8A%A1%EF%BC%89)
  * [hello world](#Hello+World)
  * [构建脚本是代码](#%E6%9E%84%E5%BB%BA%E8%84%9A%E6%9C%AC%E6%98%AF%E4%BB%A3%E7%A0%81)
  * [任务依赖性](#%E4%BB%BB%E5%8A%A1%E4%BE%9D%E8%B5%96%E6%80%A7)
  * [动态任务](#%E5%8A%A8%E6%80%81%E4%BB%BB%E5%8A%A1)
  * [处理现有任务](#%E5%A4%84%E7%90%86%E7%8E%B0%E6%9C%89%E4%BB%BB%E5%8A%A1)
  * [Groovy DSL快捷方式符号](#Groovy+DSL%E5%BF%AB%E6%8D%B7%E6%96%B9%E5%BC%8F%E7%AC%A6%E5%8F%B7)
  * [额外任务属性](#%E9%A2%9D%E5%A4%96%E4%BB%BB%E5%8A%A1%E5%B1%9E%E6%80%A7)
  * [使用Ant任务](#%E4%BD%BF%E7%94%A8Ant%E4%BB%BB%E5%8A%A1)
  * [使用方法](#%E4%BD%BF%E7%94%A8%E6%96%B9%E6%B3%95)
  * [默认任务](#%E9%BB%98%E8%AE%A4%E4%BB%BB%E5%8A%A1)
  * [通过DAG进行配置](#%E9%80%9A%E8%BF%87DAG%E8%BF%9B%E8%A1%8C%E9%85%8D%E7%BD%AE)
  * [构建脚本的外部依赖关系](#%E6%9E%84%E5%BB%BA%E8%84%9A%E6%9C%AC%E7%9A%84%E5%A4%96%E9%83%A8%E4%BE%9D%E8%B5%96%E5%85%B3%E7%B3%BB)
  * [进一步阅读](#%E8%BF%9B%E4%B8%80%E6%AD%A5%E9%98%85%E8%AF%BB)

本章向您介绍编写Gradle构建脚本的基础。要快速动手入门，请尝试以下[分步示例之一](https://docs.gradle.org/6.7.1/samples/index.html)。

## [projects（项目）与tasks（任务）](#projects%EF%BC%88%E9%A1%B9%E7%9B%AE%EF%BC%89%E4%B8%8Etasks%EF%BC%88%E4%BB%BB%E5%8A%A1%EF%BC%89)

Gradle中的所有内容都位于两个基本概念之上： _projects_ 和 _tasks_ 。

每个Gradle构建都由一个或多个 _projects组成_。
一个projects代表什么取决于您使用Gradle做的事情。
例如，一个projects可能代表一个JAR库或一个Web应用程序。
它可能表示从其他projects产生的JAR组装而成的发行版ZIP。
projects不一定代表要构建的事物
。它可能表示要完成的事情，例如将应用程序部署到暂存或生产环境。
暂时不要担心这似乎还不清楚。Gradle的按惯例构建支持为projects的定义添加了更具体的定义。

每个projects由一个或多个 _tasks组成_ 。tasks代表构建执行的一些原子工作。这可能是编译某些类，创建JAR，生成Javadoc或将一些存档发布到存储库。

现在，我们将研究在一个projects中构建一些简单的tasks。后面的章节将介绍处理多个projects，以及有关处理projects和tasks的更多信息。

## [Hello World](#Hello+World)

您可以使用以下`gradle`命令运行Gradle构建。该`gradle`命令`build.gradle`在当前目录中查找名为的文件。[[1](#_footnotedef_1"查看脚注。") ]我们严格地将其称为构建配置脚本，尽管我们将在后面看到，但我们将此`build.gradle`文件称为 _构建脚本_
。构建脚本定义projects及其tasks。

要尝试此操作，请创建以下名为的构建脚本`build.gradle`。

例子1.您的第一个构建脚本

`Groovy``Kotlin`

build.gradle

    
    
    task hello {
        doLast {
            println 'Hello world!'
        }
    }

build.gradle.kts

    
    
    tasks.register("hello") {
        doLast {
            println("Hello world!")
        }
    }

在命令行外壳中，移至包含的目录并使用以下命令执行构建脚本`gradle -q hello`：

╔═════════════════════════════

`-q`是干什么的？

本用户指南中的大多数示例都使用`-q`命令行选项运行。这样可以取消Gradle的日志消息，从而仅显示tasks的输出。这样可以使本用户指南中的示例输出更加清晰。如果不想，则不需要使用此选项。有关影响Gradle输出的命令行选项的更多详细信息，请参见[日志记录](/md/使用记录.md#logging)。  
  
╚═════════════════════════════  
  
例子2.建立脚本的执行


    Output of gradle -q hello
    
    > gradle -q hello
    Hello world!


这里发生了什么？此构建脚本定义了一个名为的任务`hello`，并向其中添加了一个动作。当您运行时`gradle
hello`，Gradle将执行`hello`任务，该tasks又将执行您提供的动作。该动作只是一个包含一些要执行代码的块。

如果您认为这看起来类似于Ant的目标，那将是正确的。Gradletasks等效于Ant目标，但正如您将看到的那样，它们的功能要强大得多。我们使用了与Ant不同的术语，因为我们认为“
_tasks_ ”一词比“ _目标_
”一词更具表现力。不幸的是，这会引起与Ant的术语冲突，因为Ant会调用其命令，例如`javac`或`copy`任务。因此，当我们谈论任务时，我们 _总是_
指Gradle任务，它等同于Ant的目标。如果我们谈论Ant任务（Ant命令），那么我们明确地说 _Ant任务_ 。

## [构建脚本是代码](#%E6%9E%84%E5%BB%BA%E8%84%9A%E6%9C%AC%E6%98%AF%E4%BB%A3%E7%A0%81)

Gradle的构建脚本为您提供了Groovy和Kotlin的全部功能。作为开胃菜，请看以下内容：

例子3.在Gradle的tasks中使用Groovy或Kotlin

`Groovy``Kotlin`

build.gradle

    
    
    task upper {
        doLast {
            String someString = 'mY_nAmE'
            println "Original: $someString"
            println "Upper case: ${someString.toUpperCase()}"
        }
    }

build.gradle.kts

    
    
    tasks.register("upper") {
        doLast {
            val someString = "mY_nAmE"
            println("Original: $someString")
            println("Upper case: ${someString.toUpperCase()}")
        }
    }


    Output of gradle -q upper
    
    > gradle -q upper
    Original: mY_nAmE
    Upper case: MY_NAME



要么

例子4.在Gradle的tasks中使用Groovy或Kotlin

`Groovy``Kotlin`

build.gradle

    
    
    task count {
        doLast {
            4.times { print "$it " }
        }
    }

build.gradle.kts

    
    
    tasks.register("count") {
        doLast {
            repeat(4) { print("$it ") }
        }
    }


Output of gradle -q count
    
    > gradle -q count
    0 1 2 3



## [任务依赖性](#%E4%BB%BB%E5%8A%A1%E4%BE%9D%E8%B5%96%E6%80%A7)

您可能已经猜到了，您可以声明依赖于其他任务的任务。

例子5.依赖于其他任务的任务声明

`Groovy``Kotlin`

build.gradle

    
    
    task hello {
        doLast {
            println 'Hello world!'
        }
    }
    task intro {
        dependsOn hello
        doLast {
            println "I'm Gradle"
        }
    }

build.gradle.kts

    
    
    tasks.register("hello") {
        doLast {
            println("Hello world!")
        }
    }
    tasks.register("intro") {
        dependsOn("hello")
        doLast {
            println("I'm Gradle")
        }
    }

输出 **`gradle -q intro`**

    
    > gradle -q intro
    Hello world!
    I'm Gradle


要添加依赖项，不需要存在相应的任务。

例子6.懒惰的dependOn-其他任务不存在（还）

`Groovy``Kotlin`

build.gradle

    
    
    task taskX {
        dependsOn 'taskY'
        doLast {
            println 'taskX'
        }
    }
    task taskY {
        doLast {
            println 'taskY'
        }
    }

build.gradle.kts

    
    
    tasks.register("taskX") {
        dependsOn("taskY")
        doLast {
            println("taskX")
        }
    }
    tasks.register("taskY") {
        doLast {
            println("taskY")
        }
    }


Output of gradle -q intro
    
    > gradle -q taskX
    taskY
    taskX


可以在定义之前声明`taskX`to的依赖项。对于多项目构建，这种自由非常重要。[将依赖项添加到任务](/md/处理任务.md#sec:adding_dependencies_to_tasks)中将详细讨论任务依赖[项](/md/处理任务.md#sec:adding_dependencies_to_tasks)。`taskY``taskY`[](/md/处理任务.md#sec:adding_dependencies_to_tasks)

请注意，在引用尚未定义的任务时，不能使用[快捷方式表示法](#Groovy+DSL%E5%BF%AB%E6%8D%B7%E6%96%B9%E5%BC%8F%E7%AC%A6%E5%8F%B7)。

## [动态任务](#%E5%8A%A8%E6%80%81%E4%BB%BB%E5%8A%A1)

Groovy或Kotlin的功能可用于定义任务以外的其他功能。例如，您还可以使用它来动态创建任务。

例子7.动态创建任务

`Groovy``Kotlin`

build.gradle

    
    
    4.times { counter ->
        task "task$counter" {
            doLast {
                println "I'm task number $counter"
            }
        }
    }

build.gradle.kts

    
    
    repeat(4) { counter ->
        tasks.register("task$counter") {
            doLast {
                println("I'm task number $counter")
            }
        }
    }

**`gradle -q task1`** 输出

    > gradle -q task1
    I'm task number 1




## [处理现有任务](#%E5%A4%84%E7%90%86%E7%8E%B0%E6%9C%89%E4%BB%BB%E5%8A%A1)

创建任务后，即可通过 _API_ 对其进行访问。例如，您可以使用它在运行时为任务动态添加依赖项。Ant不允许这样的事情。

例子8.通过API访问任务-添加依赖

`Groovy``Kotlin`

build.gradle

    
    
    4.times { counter ->
        task "task$counter" {
            doLast {
                println "I'm task number $counter"
            }
        }
    }
    task0.dependsOn task2, task3

build.gradle.kts

    
    
    repeat(4) { counter ->
        tasks.register("task$counter") {
            doLast {
                println("I'm task number $counter")
            }
        }
    }
    tasks.named("task0") { dependsOn("task2", "task3") }

**`gradle -q task0`** 输出 

    
    
    > gradle -q task0
    I'm task number 2
    I'm task number 3
    I'm task number 0

或者，您可以将行为添加到现有任务。

示例9.通过API访问任务-添加行为

`Groovy``Kotlin`

build.gradle

    
    
    task hello {
        doLast {
            println 'Hello Earth'
        }
    }
    hello.doFirst {
        println 'Hello Venus'
    }
    hello.configure {
        doLast {
            println 'Hello Mars'
        }
    }
    hello.configure {
        doLast {
            println 'Hello Jupiter'
        }
    }

build.gradle.kts

    
    
    val hello by tasks.registering {
        doLast {
            println("Hello Earth")
        }
    }
    hello {
        doFirst {
            println("Hello Venus")
        }
    }
    hello {
        doLast {
            println("Hello Mars")
        }
    }
    hello {
        doLast {
            println("Hello Jupiter")
        }
    }

输出 **`gradle -q hello`**

    
    
    > gradle -q hello
    Hello Venus
    Hello Earth
    Hello Mars
    Hello Jupiter

调用`doFirst`和`doLast`可以执行多次。他们将操作添加到任务操作列表的开始或结尾。执行任务时，将按顺序执行操作列表中的操作。

## [Groovy DSL快捷方式符号](#Groovy+DSL%E5%BF%AB%E6%8D%B7%E6%96%B9%E5%BC%8F%E7%AC%A6%E5%8F%B7)

访问 _现有_ 任务有一种方便的表示法。每个任务都可以作为构建脚本的属性来使用：

例子10.作为构建脚本的属性访问任务

build.gradle

    
    
    task hello {
        doLast {
            println 'Hello world!'
        }
    }
    hello.doLast {
        println "Greetings from the $hello.name task."
    }

输出 **`gradle -q hello`**

    
    
    > gradle -q hello
    Hello world!
    Greetings from the hello task.

这将启用非常可读的代码，尤其是在使用插件提供的任务（例如`compile`任务）时。

## [额外任务属性](#%E9%A2%9D%E5%A4%96%E4%BB%BB%E5%8A%A1%E5%B1%9E%E6%80%A7)

您可以将自己的属性添加到任务。要添加名为的属性`myProperty`，请设置`ext.myProperty`为初始值。从那时起，可以像预定义的任务属性一样读取和设置属性。

例子11.向任务添加额外的属性

`Groovy``Kotlin`

build.gradle

    
    
    task myTask {
        ext.myProperty = "myValue"
    }
    
    task printTaskProperties {
        doLast {
            println myTask.myProperty
        }
    }

build.gradle.kts

    
    
    tasks.register("myTask") {
        extra["myProperty"] = "myValue"
    }
    
    tasks.register("printTaskProperties") {
        doLast {
            println(tasks["myTask"].extra["myProperty"])
        }
    }

输出 **`gradle -q printTaskProperties`**

    
    
    > gradle -q printTaskProperties
    myValue

额外的属性不仅限于任务。您可以在[Extra属性中](/md/编写构建脚本.md#sec:extra_properties)阅读有关它们的更多信息。

## [使用Ant任务](#%E4%BD%BF%E7%94%A8Ant%E4%BB%BB%E5%8A%A1)

Ant任务是Gradle中的一等公民。仅仅依靠Groovy，Gradle就可以为Ant任务提供出色的集成。Groovy附带了出色的功能`AntBuilder`。与从`build.xml`文件中使用Ant任务相比，使用Gradle中的Ant任务既方便又强大。它也可以从Kotlin使用。从下面的示例中，您可以学习如何执行Ant任务以及如何访问Ant属性：

例子12.使用AntBuilder执行ant.loadfile目标

`Groovy``Kotlin`

build.gradle

    
    
    task loadfile {
        doLast {
            def files = file('./antLoadfileResources').listFiles().sort()
            files.each { File file ->
                if (file.isFile()) {
                    ant.loadfile(srcFile: file, property: file.name)
                    println " *** $file.name ***"
                    println "${ant.properties[file.name]}"
                }
            }
        }
    }

build.gradle.kts

    
    
    tasks.register("loadfile") {
        doLast {
            val files = file("./antLoadfileResources").listFiles().sorted()
            files.forEach { file ->
                if (file.isFile) {
                    ant.withGroovyBuilder {
                        "loadfile"("srcFile" to file, "property" to file.name)
                    }
                    println(" *** ${file.name} ***")
                    println("${ant.properties[file.name]}")
                }
            }
        }
    }

输出 **`gradle -q loadfile`**

    
    
    > gradle -q loadfile
     *** agile.manifesto.txt ***
    Individuals and interactions over processes and tools
    Working software over comprehensive documentation
    Customer collaboration  over contract negotiation
    Responding to change over following a plan
     *** gradle.manifesto.txt ***
    Make the impossible possible, make the possible easy and make the easy elegant.
    (inspired by Moshe Feldenkrais)

您可以在构建脚本中使用Ant进行更多操作。您可以在[Ant中](/md/从Gradle使用Ant.md#ant)找到更多信息。

## [使用方法](#%E4%BD%BF%E7%94%A8%E6%96%B9%E6%B3%95)

Gradle扩展了如何组织构建逻辑。上面示例中组织构建逻辑的第一级是提取方法。

例子13.使用方法组织构建逻辑

`Groovy``Kotlin`

build.gradle

    
    
    task checksum {
        doLast {
            fileList('./antLoadfileResources').each { File file ->
                ant.checksum(file: file, property: "cs_$file.name")
                println "$file.name Checksum: ${ant.properties["cs_$file.name"]}"
            }
        }
    }
    
    task loadfile {
        doLast {
            fileList('./antLoadfileResources').each { File file ->
                ant.loadfile(srcFile: file, property: file.name)
                println "I'm fond of $file.name"
            }
        }
    }
    
    File[] fileList(String dir) {
        file(dir).listFiles({file -> file.isFile() } as FileFilter).sort()
    }

build.gradle.kts

    
    
    tasks.register("checksum") {
        doLast {
            fileList("./antLoadfileResources").forEach { file ->
                ant.withGroovyBuilder {
                    "checksum"("file" to file, "property" to "cs_${file.name}")
                }
                println("$file.name Checksum: ${ant.properties["cs_${file.name}"]}")
            }
        }
    }
    
    tasks.register("loadfile") {
        doLast {
            fileList("./antLoadfileResources").forEach { file ->
                ant.withGroovyBuilder {
                    "loadfile"("srcFile" to file, "property" to file.name)
                }
                println("I'm fond of ${file.name}")
            }
        }
    }
    
    fun fileList(dir: String): List<File> =
        file(dir).listFiles { file: File -> file.isFile }.sorted()

输出 **`gradle -q loadfile`**

    
    
       > gradle -q loadfile
       I'm fond of agile.manifesto.txt
       I'm fond of gradle.manifesto.txt

稍后您将看到，这些方法可以在多项目构建中的子项目之间共享。如果您的构建逻辑变得更加复杂，Gradle将为您提供其他非常方便的组织方式。我们为此花了整整一章。请参阅[组织Gradle项目](/md/组织Gradle项目.md#organizing_gradle_projects)。

## [默认任务](#%E9%BB%98%E8%AE%A4%E4%BB%BB%E5%8A%A1)

如果未指定其他任务，则Gradle允许您定义一个或多个默认任务。

例子14.定义一个默认任务

`Groovy``Kotlin`

build.gradle

    
    
    defaultTasks 'clean', 'run'
    
    task clean {
        doLast {
            println 'Default Cleaning!'
        }
    }
    
    task run {
        doLast {
            println 'Default Running!'
        }
    }
    
    task other {
        doLast {
            println "I'm not a default task!"
        }
    }

build.gradle.kts

    
    
    defaultTasks("clean", "run")
    
    task("clean") {
        doLast {
            println("Default Cleaning!")
        }
    }
    
    tasks.register("run") {
        doLast {
            println("Default Running!")
        }
    }
    
    tasks.register("other") {
        doLast {
            println("I'm not a default task!")
        }
    }

输出 **`gradle -q`**

    
    
    > gradle -q
    Default Cleaning!
    Default Running!

这等效于运行`gradle clean
run`。在多项目构建中，每个子项目可以有其自己的特定默认任务。如果子项目未指定默认任务，则使用父项目的默认任务（如果已定义）。

## [通过DAG进行配置](#%E9%80%9A%E8%BF%87DAG%E8%BF%9B%E8%A1%8C%E9%85%8D%E7%BD%AE)

正如我们稍后将详细描述的（请参阅[Build
Lifecycle](/md/构建生命周期.md#build_lifecycle)），Gradle具有配置阶段和执行阶段。在配置阶段之后，Gradle知道应该执行的所有任务。Gradle为您提供了一个利用此信息的机会。一个用例是检查发布任务是否在要执行的任务中。以此为基础，您可以为某些变量分配不同的值。

在以下示例中，`distribution`和`release`任务的执行导致`version`变量的值不同。

例子15.取决于所选任务的不同构建结果

`Groovy``Kotlin`

build.gradle

    
    
    task distribution {
        doLast {
            println "We build the zip with version=$version"
        }
    }
    
    task release {
        dependsOn 'distribution'
        doLast {
            println 'We release now'
        }
    }
    
    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(":release")) {
            version = '1.0'
        } else {
            version = '1.0-SNAPSHOT'
        }
    }

build.gradle.kts

    
    
    tasks.register("distribution") {
        doLast {
            println("We build the zip with version=$version")
        }
    }
    
    tasks.register("release") {
        dependsOn("distribution")
        doLast {
            println("We release now")
        }
    }
    
    gradle.taskGraph.whenReady {
        version =
            if (hasTask(":release")) "1.0"
            else "1.0-SNAPSHOT"
    }

输出 `gradle -q distribution`

    
    
    > gradle -q distribution
    We build the zip with version=1.0-SNAPSHOT

输出 `gradle -q release`

    
    
       > gradle -q release
       We build the zip with version=1.0
       We release now

重要的是，这`whenReady`会影响执行释放任务 _之前_ 的释放任务。即使释放任务不是 _主要_
任务（即，传递给`gradle`命令的任务），此方法也有效。

╔═════════════════════════════

此示例有效，因为该`version`值仅在执行时读取。在实际构建中使用类似的构造时，必须确保在配置过程中不会急切读取该值。否则，您的构建可能会对配置和执行之间的属性使用不同的值。  
  
╚═════════════════════════════  
  
## [构建脚本的外部依赖关系](#%E6%9E%84%E5%BB%BA%E8%84%9A%E6%9C%AC%E7%9A%84%E5%A4%96%E9%83%A8%E4%BE%9D%E8%B5%96%E5%85%B3%E7%B3%BB)

如果构建脚本需要使用外部库，则可以将它们添加到构建脚本本身中的脚本的类路径中。您可以使用`buildscript()`方法执行此操作，并传入一个声明构建脚本类路径的块。

例子16.声明构建脚本的外部依赖

`Groovy``Kotlin`

build.gradle

    
    
    buildscript {
        repositories {
            mavenCentral()
        }
        dependencies {
            classpath group: 'commons-codec', name: 'commons-codec', version: '1.2'
        }
    }

build.gradle.kts

    
    
    buildscript {
        repositories {
            mavenCentral()
        }
        dependencies {
            "classpath"(group = "commons-codec", name = "commons-codec", version = "1.2")
        }
    }

传递给该`buildscript()`方法的块将配置[ScriptHandler](https://docs.gradle.org/6.7.1/javadoc/org/gradle/api/initialization/dsl/ScriptHandler.html)实例。您可以通过向`classpath`配置添加依赖项来声明构建脚本类路径。例如，这与声明Java编译类路径的方式相同。您可以使用除项目依赖项以外的任何[依赖项类型](/md/声明依赖.md#sec:dependency-
types)。

声明了构建脚本类路径后，就可以像在该类路径上的任何其他类一样使用构建脚本中的类。以下示例将添加到前面的示例中，并使用构建脚本类路径中的类。

例子17.具有外部依赖关系的构建脚本

`Groovy``Kotlin`

build.gradle

    
    
    import org.apache.commons.codec.binary.Base64
    
    buildscript {
        repositories {
            mavenCentral()
        }
        dependencies {
            classpath group: 'commons-codec', name: 'commons-codec', version: '1.2'
        }
    }
    
    task encode {
        doLast {
            def byte[] encodedString = new Base64().encode('hello world\n'.getBytes())
            println new String(encodedString)
        }
    }

build.gradle.kts

    
    
    import org.apache.commons.codec.binary.Base64
    
    buildscript {
        repositories {
            mavenCentral()
        }
        dependencies {
            "classpath"(group = "commons-codec", name = "commons-codec", version = "1.2")
        }
    }
    
    tasks.register("encode") {
        doLast {
            val encodedString = Base64().encode("hello world\n".toByteArray())
            println(String(encodedString))
        }
    }

输出 **`gradle -q encode`**

    > gradle -q encode
    aGVsbG8gd29ybGQK

对于多项目构建，使用projects`buildscript()`方法声明的依赖项可用于其所有子项目的构建脚本。

构建脚本依赖项可能是Gradle插件。请参阅[使用Gradle插件](/md/使用Gradle插件.md#plugins)以获取有关Gradle插件的更多信息。

每个项目都会自动具有[BuildEnvironmentReportTask](https://docs.gradle.org/6.7.1/dsl/org.gradle.api.tasks.diagnostics.BuildEnvironmentReportTask.html)`buildEnvironment`类型的任务，可以调用该任务来报告构建脚本依赖项的解决方案。[](https://docs.gradle.org/6.7.1/dsl/org.gradle.api.tasks.diagnostics.BuildEnvironmentReportTask.html)

## [进一步阅读](#%E8%BF%9B%E4%B8%80%E6%AD%A5%E9%98%85%E8%AF%BB)

本章仅介绍了可能的内容。以下是一些可能有趣的主题：

  * [编写可维护的构建脚本](/md/创作可维护版本的最佳实践.md#authoring_maintainable_build_scripts)

  * [整理您的Gradle项目](/md/组织Gradle项目.md#organizing_gradle_projects)

  * [编写自定义任务](/md/开发自定义Gradle任务类型.md#custom_tasks)

* * *

[1](#_footnoteref_1)。有命令行开关可以更改此行为。请参阅[命令行界面](/md/命令行界面.md#command_line_interface)）
